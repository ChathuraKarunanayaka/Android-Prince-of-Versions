apply plugin: 'com.android.library'
apply from: './quality.gradle'
apply plugin: 'com.novoda.bintray-release'

ext {
    publishedGroupId = 'co.infinum'
    publishedArtifactName = 'prince-of-versions'
    libraryVersion = '3.0.0'
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName libraryVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    resourcePrefix 'prince_of_versions_'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.github.zafarkhaja:java-semver:0.9.0'
    compileOnly 'com.android.support:appcompat-v7:27.1.1'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'org.json:json:20140107'
    testCompileOnly 'com.google.code.findbugs:annotations:3.0.1'
}

group = publishedGroupId
version = libraryVersion

// https://github.com/novoda/bintray-release/wiki/Configuration-of-the-publish-closure
publish {
    userOrg = 'infinum'
    groupId = publishedGroupId
    artifactId = publishedArtifactName
    publishVersion = libraryVersion
    desc = 'Library checks for application updates using configuration from some resource.'
    website = 'https://github.com/infinum/Android-prince-of-versions'
    licences = ["Apache-2.0"]
    repoName = 'android'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

